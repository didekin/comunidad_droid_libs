apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        testInstrumentationRunner rootProject.testInstrumentationRunner
        testInstrumentationRunnerArguments clearPackageData: 'true'
        resConfigs "es" /* , "en"  Añadir nuevos idiomas según hagan falta.*/
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
    testOptions {
        if (rootProject.orchestrator) {
            execution "ANDROID_TEST_ORCHESTRATOR"
        }
    }

//    Activar antes de pasar a PRE.
//    testBuildType "debugPre"

    buildTypes {
        release {
            minifyEnabled false
        }
        debugPre {
            initWith debug
            // Variables for android_studio default run-debug configuration.
            if ("$System.env.TEST_HOST" == "null" || "$System.env.TEST_PORT" == "null") {
                resValue "string", "didekin_web_host", '"https://didekin-web-pre.eu-west-1.elasticbeanstalk.com"'
                resValue "string", "didekin_web_port", '":443"'
            } else {
                resValue "string", "didekin_web_host", "$System.env.TEST_HOST"
                resValue "string", "didekin_web_port", "$System.env.TEST_PORT"
            }
        }
        debug {
            debuggable true
            versionNameSuffix ''
            // Variables for android_studio default run-debug configuration.
            if ("$System.env.TEST_HOST" == "null" || "$System.env.TEST_PORT" == "null") {
                // LOCAL url's
                resValue "string", "didekin_web_host", '"https://didekinspring.pagekite.me"'
                resValue "string", "didekin_web_port", '""'
            } else {
                resValue "string", "didekin_web_host", "$System.env.TEST_HOST"
                resValue "string", "didekin_web_port", "$System.env.TEST_PORT"
            }
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError true
        quiet true
        ignoreWarnings false
        htmlReport true
//        disable  "InvalidPackage"
    }

    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        pickFirst 'protobuf.meta'
    }
}

dependencies {
    implementation rootProject.firebase_msg
    implementation rootProject.didekinlib_model
    implementation rootProject.didekinlib_utilities
    implementation rootProject.timber
    implementation rootProject.retrofit2
    implementation rootProject.rxjava
    implementation rootProject.android_support
    androidTestImplementation rootProject.testRuler
    androidTestImplementation rootProject.testRunner
    androidTestImplementation rootProject.espresso
    androidTestImplementation rootProject.awaitility
    androidTestUtil rootProject.testOrchestrator
    androidTestImplementation rootProject.junit
}

apply plugin: 'com.google.gms.google-services'